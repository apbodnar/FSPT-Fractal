<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8">
<title>Blinn Phong Ping Pong</title>
<style>
  body{ text-align: center; margin: 0; overflow: hidden; background: #000;}
  canvas{ border: none; }
</style>
<script id="FRAGMENT_SHADER" type="x-shader/x-fragment">
  precision highp float;
  const int sphereCount = 14;
  const float max_t = 100000.0;
  const float n1 = 1.0;
  const float n2 = 1.458;
  const float sr = n1/n2;
  const float r0 = ((n1 - n2)/(n1 + n2))*((n1 - n2)/(n1 + n2));
  const float M_PI = 3.1415926535897932384626433832795;
  const float epsilon = 0.00001; //not really epsilon
  const float gamma = 1.0/2.2;
  const int maxSteps = 100;
  const float minDistance = 0.0001;

  varying vec2 coords;
  uniform int mode;
  uniform int tick;
  uniform vec2 dims;
  uniform vec3 eye;
  uniform vec3 spherePositions[sphereCount];
  uniform vec3 sphereAttrs[sphereCount];
  uniform vec3 sphereMats[sphereCount];
  uniform vec3 sphereColors[sphereCount];
  uniform sampler2D fbTex;

  float tick_f = float(tick);

  struct Sphere {
    vec3 origin;
    vec3 attrs;
    vec3 color;
    vec3 material;
  };

  struct Ray{
    vec3 origin;
    vec3 dir;
  };

  struct Hit{
    Ray ray;
    vec3 emittance;
    vec3 reflectance;
  };

  float rand(vec2 co){
    float a = 12.9898;
    float b = 78.233;
    float c = 43758.5453;
    float dt= dot(co ,vec2(a,b) + tick_f*0.0194161103873);
    float sn= mod(dt,M_PI);
    return fract(sin(sn) * c);
  }

  float getAngle(vec3 a){
    vec3 b = vec3(0.0,0.0,1.0);
    return atan(length(cross(a,b)),a.z);
  }

  mat3 rotationMatrix(vec3 axis, float angle){
    axis = normalize(axis);
    float s = sin(angle);
    float c = cos(angle);
    float oc = 1.0 - c;
    return mat3(oc * axis.x * axis.x + c,    oc * axis.x * axis.y - axis.z * s, oc * axis.z * axis.x + axis.y * s,
          oc * axis.x * axis.y + axis.z * s, oc * axis.y * axis.y + c,          oc * axis.y * axis.z - axis.x * s,
          oc * axis.z * axis.x - axis.y * s, oc * axis.y * axis.z + axis.x * s, oc * axis.z * axis.z + c);
  }

  vec3 randomVec(vec3 normal, vec3 origin, float exp){
    float r2 = rand(origin.xz);
    float r1 = rand(origin.xy)-epsilon;
    float r = pow(r1,exp);
    float theta = 2.0 * M_PI * r2;
    float x = r * cos(theta);
    float y = r * sin(theta);
    vec3 rv = vec3(x, y, sqrt(1.0 - r*r));
    float phi = getAngle(normal);
    return rotationMatrix(cross(normal,vec3(0.0,0.0,1.0)),phi) * rv;
  }

  Hit getLambertian(int i, float t, Ray ray, Sphere s){
    Hit result;
    result.ray.origin = ray.dir*t + ray.origin;
    vec3 normal = normalize(result.ray.origin - s.origin);
    result.emittance = s.attrs.z * s.color;
    result.ray.dir = randomVec(normal, result.ray.origin, 0.5);
    result.reflectance = s.color;
    return result;
  }

  // float distanceEstimator(vec3 z){
  //   z.xy = mod((z.xy),0.1)-vec2(0.05); // instance on xy-plane
  //   return length(z)-0.01;             // sphere DE
  // }

  float primitive(vec3 p){
    vec2 t = vec2(0.15,0.075);
    vec2 q = vec2(length(p.xy)-t.x,p.z);
    return length(q)-t.y;
  }

  float distanceEstimator( vec3 p ){
    p+=vec3(0.25,0.25,0);
    p = (mod((p),0.5)-vec3(0.25));

    return primitive(p);
  }

  vec3 getNormal(vec3 p){
    float eps = 0.0001;
    vec3 epsx = vec3(eps,0,0);
    vec3 epsy = vec3(0,eps,0);
    vec3 epsz = vec3(0,0,eps);
    return normalize(vec3(
      distanceEstimator(p+epsx)-distanceEstimator(p-epsx),
      distanceEstimator(p+epsy)-distanceEstimator(p-epsy),
      distanceEstimator(p+epsz)-distanceEstimator(p-epsz))
    );
  }

  float trace(Ray ray) {
    float totalDistance = 0.0;
    int tsteps;
    for (int steps = 0; steps < maxSteps; steps++) {
      tsteps = steps;
      vec3 p = ray.origin + totalDistance * ray.dir;
      float distance = distanceEstimator(p);
      totalDistance += distance;
      if (distance < minDistance) break;
    }
    return totalDistance;
  }

  vec3 getEmittance(vec3 p){
    vec3 emittance = vec3(0);
    float bound = 0.1;
    if(distance(p, vec3(0.5,0.5,-0.1)) < 0.35){
      emittance = vec3(100.0);
    }
    return emittance;
  }

  vec3 getReflectance(vec3 p){
    float d = 20.0*dot(p, vec3(0,0,1));
    return vec3(sin(d),sin(d+1.55),sin(d+3.14));
  }

  Hit getMat(Ray ray){
    float t = trace(ray);
    vec3 p = ray.origin + ray.dir*t;
    vec3 normal = getNormal(p);
    vec3 dir = randomVec(normal,p,0.5);
    p+=dir*minDistance;
    return Hit(Ray(p,dir),getEmittance(p),getReflectance(p));
  }

  void main(void) {
    vec3 tcolor = texture2D(fbTex,coords*0.5 + 0.5).rgb;
    if(mode == 0){
      float inv_dim = 1.0 / dims.x;
      vec2 dof = vec2(rand(coords)*inv_dim, rand(coords.yx)*inv_dim);
      vec3 origin = vec3(coords.x*dims.x*inv_dim + dof.x,coords.y + dof.y,0);
      Ray ray = Ray(origin,normalize(origin - eye));
      Hit h0 = getMat(ray);
      Hit h1 = getMat(h0.ray);
      Hit h2 = getMat(h1.ray);      
      Hit h3 = getMat(h2.ray);
      Hit h4 = getMat(h3.ray);
      Hit h5 = getMat(h4.ray);
      //No recursion in GLSL
      vec3 color = (h0.emittance + h0.reflectance *
                  (h1.emittance + h1.reflectance *
                  (h2.emittance + h2.reflectance *
                  (h3.emittance + h3.reflectance *
                  (h4.emittance + h4.reflectance *
                  (h5.emittance))))));
      //         (r3.emittance + r3.reflectance *
      //         (r4.emittance + r4.reflectance *
      //         (r5.emittance + r5.reflectance *
      //         (r6.emittance)))))));
//color = h1.emittance;

      gl_FragColor = vec4((color + (tcolor * tick_f))/(tick_f+1.0),1.0);
    } else {
      gl_FragColor = vec4(tcolor,1.0);
    }
  }
</script>

<script id="VERTEX_SHADER" type="x-shader/x-vertex">
  precision highp float;
  attribute vec3 corner;
  varying vec2 coords;
  void main(void) {
    coords = corner.xy;
    gl_Position = vec4(corner, 1.0);
  }
</script>
<script src="fspt-fractal.js" type="text/javascript"></script>
</head>


<body onload="webGLStart();">
  <canvas id="trace" style="border: none;"></canvas>
</body>

</html>
